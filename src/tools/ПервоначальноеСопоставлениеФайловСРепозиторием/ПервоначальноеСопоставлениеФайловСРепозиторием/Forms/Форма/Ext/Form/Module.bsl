#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДополнительныеОтчетыИОбработки.Ссылка КАК Файл,
	|	ДополнительныеОтчетыИОбработки.ИмяФайла КАК ИмяФайла,
	|	"""" КАК АдресВРепозитории
	|ИЗ
	|	Справочник.ДополнительныеОтчетыИОбработки КАК ДополнительныеОтчетыИОбработки
	|ГДЕ
	|	ДополнительныеОтчетыИОбработки.Публикация = ЗНАЧЕНИЕ(Перечисление.ВариантыПубликацииДополнительныхОтчетовИОбработок.Используется)
	|	И НЕ ДополнительныеОтчетыИОбработки.ПометкаУдаления
	|	И ДополнительныеОтчетыИОбработки.ИмяФайла <> """"
	|	И ДополнительныеОтчетыИОбработки.SslCI_АдресВРепозитории = """"";
	
    Таблица.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВсеФайлы = SslCI_РаботаСГитом.ПолучитьДанныеРепозитория("master");
	
	ФайлыСоответствием = Новый Соответствие;
	
	Для Каждого Файл Из ВсеФайлы Цикл
		Если СтрНайти(Файл, "/Архив/") <> 0
			Или СтрНайти(Файл, "/Разовые/") <> 0
			Или СтрНайти(Файл, "/Служебные/") <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ФайлыСоответствием.Вставить(ИмяФайлаБезПути(Файл), Файл);
	КонецЦикла;
	
	Для Каждого СтрокаДанных Из Таблица Цикл
		
		ИмяФайла = СтрЗаменить(СтрокаДанных.ИмяФайла, " (1)", "");
		ИмяФайла = СтрЗаменить(ИмяФайла, " (2)", "");
		ИмяФайла = СтрЗаменить(ИмяФайла, " (3)", "");
		
		Поиск = ФайлыСоответствием.Получить(ИмяФайла);
		
		Если Поиск <> Неопределено Тогда
			СтрокаДанных.АдресВРепозитории = Поиск;	
		КонецЕсли;		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьСервер();
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ИмяФайлаБезПути(ПутьКФайлу)
	
	СтруктураФайла = СтрРазделить(ПутьКФайлу, "/", Ложь);
	
	ИмяФайла = СтруктураФайла.Получить(СтруктураФайла.Количество() - 1);
	
	Возврат ИмяФайла;
	
КонецФункции

&НаСервере
Процедура ЗаписатьСервер()
	
	Для Каждого СтрокаДанных Из Таблица Цикл
		Если ЗначениеЗаполнено(СтрокаДанных.АдресВРепозитории) Тогда
			СпрОбъект = СтрокаДанных.Файл.ПолучитьОбъект();
			СпрОбъект.SslCI_АдресВРепозитории = СтрокаДанных.АдресВРепозитории;
			СпрОбъект.SslCI_ИмяВетки = "master";
			СпрОбъект.Записать();			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти