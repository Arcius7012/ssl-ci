////////////////////////////////////////////////////////////////////////////////
// Проверка соединения с гитом.
//  
////////////////////////////////////////////////////////////////////////////////
#Область СлужебныйПрограммныйИнтерфейс

Функция СоединениеСГитомУстановлено(НастройкиГита = Неопределено, ПоказыватьУведомления = Ложь) Экспорт
	
	Если НастройкиГита = Неопределено Тогда
		НастройкиГита = SslCI_СлужебныйВызовСервера.ПолучитьТекущиеНастройкиИнтеграции();
	КонецЕсли;
	
	Если НастройкиГита = Неопределено Тогда
		ОповеститьПользователя(ПоказыватьУведомления, "Не заданы настройки для соединения");
		Возврат Ложь;	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НастройкиГита.АдресГита)
		Или Не ЗначениеЗаполнено(НастройкиГита.ИдПроекта)
		Или Не ЗначениеЗаполнено(НастройкиГита.ТокенДоступа) Тогда
		ОповеститьПользователя(ПоказыватьУведомления, "Не заданы настройки для соединения");
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеГита = SslCI_ДанныеГита.ПолучитьДанныеГита("user", , НастройкиГита);
	ИмяСобытия = SslCI_НастройкаИнтеграцииСГитомПовтИсп.ИмяСобытияЖР();	
	
	Если ДанныеГита.Успех Тогда
		ОповеститьПользователя(ПоказыватьУведомления, "Соединение установлено");
	Иначе
		ОповеститьПользователя(ПоказыватьУведомления, "Соединение не установлено. см.ЖР");		
		
		#Если ТонкийКлиент Тогда
			ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(ИмяСобытия, "Ошибка", ДанныеГита.Результат, , Истина);
		#Иначе
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ДанныеГита.Результат);
		#КонецЕсли
		
		Возврат Ложь;
	КонецЕсли;
	
	// Проверяем правильно ли указан ИД проекта 
	ДанныеГита = SslCI_ДанныеГита.ПолучитьДанныеГита("/projects/" + Формат(НастройкиГита.ИдПроекта, "ЧГ=0") + "/repository/contributors", , НастройкиГита);
	
	Если ДанныеГита.Успех Тогда
		ОповеститьПользователя(ПоказыватьУведомления, "Проект в репозитории найден");
		Возврат Истина;
	Иначе
		ОповеститьПользователя(ПоказыватьУведомления, "Не найден проект в репозитории. см.ЖР");
		
		#Если ТонкийКлиент Тогда
			ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(ИмяСобытия, "Ошибка", ДанныеГита.Результат, , Истина);
		#Иначе
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ДанныеГита.Результат);
		#КонецЕсли
		
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ИдПроекта(Числом = Истина) Экспорт

	ИдПроекта = ОпределитьЗначение("ИдПроекта", 0);	
	
	Если Числом Тогда
		Возврат Формат(ИдПроекта, "ЧГ=0");
	Иначе
		Возврат ИдПроекта;
	КонецЕсли;
	
КонецФункции

Функция АдресГита() Экспорт
	
	Возврат ОпределитьЗначение("АдресГита", "");
		
КонецФункции

Функция КаталогИсходников() Экспорт
	
	НастройкиГита = SslCI_СлужебныйВызовСервера.ПолучитьТекущиеНастройкиИнтеграции();
	
	Возврат НастройкиГита.КаталогИсходников;
	
КонецФункции

Функция АдресТаскТрекера() Экспорт 
	
	Возврат ОпределитьЗначение("АдресТаскТрекера", "");	
	
КонецФункции

Функция ПрефиксЗадачи() Экспорт 
	
	Возврат ОпределитьЗначение("ПрефиксЗадачи", "");	
	
КонецФункции

Функция ПутьКРепозиторию() Экспорт
	
	Возврат ОпределитьЗначение("ПутьКРепозиторию", "");	
	
КонецФункции

Функция РучноеДобавлениеФайлов() Экспорт
	
	Возврат ОпределитьЗначение("РучноеДобавлениеФайлов", Истина);
	
КонецФункции

Функция ИмяСобытияЖР() Экспорт

	Возврат "Интеграция с гитом";
	
КонецФункции

Функция НастройкиПоискаЗадачТрекер() Экспорт
	
	НастройкиТрекера = Новый Структура;
	
	СтрокаПоискаТрекер = ОпределитьЗначение("СтрокаПоискаТрекер", "");
	ЛогинДоступаТрекер = ОпределитьЗначение("ЛогинДоступаТрекер", "");
	ПарольДоступаТрекер = ОпределитьЗначение("ПарольДоступаТрекер", "");
	
	НастройкиТрекера.Вставить("СтрокаПоискаТрекер", СтрокаПоискаТрекер);
	НастройкиТрекера.Вставить("ЛогинДоступаТрекер", ЛогинДоступаТрекер);
	НастройкиТрекера.Вставить("ПарольДоступаТрекер", ПарольДоступаТрекер);
	
	Возврат НастройкиТрекера;
	
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ОповеститьПользователя(ПоказыватьУведомления, ОписаниеСобытия)
	
	#Если ТонкийКлиент Тогда
	Если ПоказыватьУведомления Тогда
		ПоказатьОповещениеПользователя(ОписаниеСобытия, , , БиблиотекаКартинок.SslCI_Гит, СтатусОповещенияПользователя.Важное);
	КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

Функция ОпределитьЗначение(ИмяНастройки, ЗначениеПоУмолчанию)
	
	НастройкиГита = SslCI_СлужебныйВызовСервера.ПолучитьТекущиеНастройкиИнтеграции();
	
	Если НастройкиГита = Неопределено
		Или Не НастройкиГита.Свойство(ИмяНастройки) Тогда
		Возврат ЗначениеПоУмолчанию;
	Иначе
		Возврат НастройкиГита[ИмяНастройки];
	КонецЕсли;	
	
КонецФункции

#КонецОбласти