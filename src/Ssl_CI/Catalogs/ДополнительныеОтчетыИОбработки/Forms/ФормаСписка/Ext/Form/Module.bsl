#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура SslCI_ОбработкаВыбораПосле(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Справочник.ДополнительныеОтчетыИОбработки.Форма.SslCI_ПереключениеВетки" Тогда
		
		SslCI_РаботаСГитом.АктуализацияСправочника(ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура SslCI_ПриОткрытииПосле(Отказ)	
	ВидимостьРучногоДобавленияФайлов();	
КонецПроцедуры

&НаКлиенте
Процедура SslCI_ОбработкаОповещенияПосле(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеНастроекГита" Тогда
		ВидимостьРучногоДобавленияФайлов();	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура SslCI_СписокВыборПосле(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	
	Если Поле = Элементы.SslCI_ЗадачаВТаскТрекере Тогда
		СтандартнаяОбработка = Ложь;
		SslCI_РаботаСГитом.ОткрытьЗадачуВТрекере(ТекДанные.SslCI_СообщениеКоммита);
	КонецЕсли;
	
	Если Поле = Элементы.SslCI_Коммит Тогда
		СтандартнаяОбработка = Ложь;
		SslCI_РаботаСГитом.ОткрытьКоммит(ТекДанные.SslCI_ИДКоммита);	
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти   

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура SslCI_sslСоздатьПосле(Команда)
	
	ПараметрыСоздания = Новый Структура("ИзРепозитория", Истина);
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные.ЭтоГруппа Тогда
		ПараметрыСоздания.Вставить("Родитель", ТекущиеДанные.Ссылка);	
	Иначе
		ПараметрыСоздания.Вставить("Родитель", ТекущиеДанные.Родитель);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ДополнительныеОтчетыИОбработки.Форма.ФормаЭлемента", 
		ПараметрыСоздания, ЭтотОбъект, Истина, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВидимостьРучногоДобавленияФайлов()
	
	РучноеДобавлениеФайлов = SslCI_НастройкаИнтеграцииСГитомПовтИсп.РучноеДобавлениеФайлов();
	
	Элементы.Создать.Видимость 				= РучноеДобавлениеФайлов;
	Элементы.ЗагрузитьИзФайла.Видимость 	= РучноеДобавлениеФайлов;
	Элементы.СоздатьМеню.Видимость 			= РучноеДобавлениеФайлов;
	Элементы.ЗагрузитьИзФайлаМеню.Видимость = РучноеДобавлениеФайлов;
	
КонецПроцедуры

#КонецОбласти