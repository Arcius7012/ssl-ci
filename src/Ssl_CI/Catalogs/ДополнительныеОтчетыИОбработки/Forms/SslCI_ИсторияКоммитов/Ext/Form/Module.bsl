#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Файл = Параметры.Файл;
	
	РеквизитыФайла = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Файл, 
		"SslCI_АдресВРепозитории, SslCI_ИДКоммита, SslCI_ИмяВетки, SslCI_АвторКоммита, SslCI_ДатаКоммита, SslCI_СообщениеКоммита");
	
	ИмяФайла 	= РеквизитыФайла.SslCI_АдресВРепозитории;
	Ветка 		= РеквизитыФайла.SslCI_ИмяВетки;
	ТекКоммит	= РеквизитыФайла.SslCI_ИДКоммита;
	
	АвторКоммита 	 = РеквизитыФайла.SslCI_АвторКоммита;
	ДатаКоммита 	 = РеквизитыФайла.SslCI_ДатаКоммита;
	СообщениеКоммита = РеквизитыФайла.SslCI_СообщениеКоммита;
	
	Если ИмяФайла = ""
		Или Ветка = "" Тогда
		ТекстСообщения = НСтр("ru='Файл не привязан к репозиторию'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ДанныеФайла = SslCI_РаботаСГитом.ИсторияКоммитов(ИмяФайла, Ветка);
	
	Если ДанныеФайла.Успех Тогда
		
		ЕстьТекущий = Ложь;
		Для Каждого Коммит Из ДанныеФайла.Коммиты Цикл
			НовыйКоммит = Коммиты.Добавить();
			НовыйКоммит.ИдКоммита 			= Коммит.id;
			НовыйКоммит.АвторКоммита 		= Коммит.committer_name + " <" + Коммит.committer_email + ">";
			НовыйКоммит.ДатаКоммита 		= Коммит.committed_date;
			НовыйКоммит.СообщениеКоммита 	= Коммит.message;
			
			НовыйКоммит.ЗадачаВТаскТрекере	= "см.задачу";
			НовыйКоммит.Коммит = "см.коммит";
			НовыйКоммит.ПерейтиНаКоммит = "откатить до коммита";
			
			Если Коммит.id = ТекКоммит Тогда
				ЕстьТекущий = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ЕстьТекущий Тогда
			НовыйКоммит = Коммиты.Добавить();
			НовыйКоммит.ИдКоммита 		 = ТекКоммит;
			НовыйКоммит.АвторКоммита 	 = АвторКоммита;
			НовыйКоммит.ДатаКоммита 	 = ДатаКоммита;
			НовыйКоммит.СообщениеКоммита = СообщениеКоммита;
			
			НовыйКоммит.ЗадачаВТаскТрекере	= "см.задачу";
			НовыйКоммит.Коммит = "см.коммит";
			НовыйКоммит.ПерейтиНаКоммит = "откатить до коммита";
		КонецЕсли;
		
		Коммиты.Сортировать("ДатаКоммита");
	Иначе
		ТекстСообщения = НСтр("ru='Не найдена информация о коммитах'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКоммиты

&НаКлиенте
Процедура КоммитыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекДанные = Элементы.Коммиты.ТекущиеДанные;
	
	Если Поле = Элементы.КоммитыЗадачаВТаскТрекере Тогда
		SslCI_РаботаСГитом.ОткрытьЗадачуВТрекере(ТекДанные.СообщениеКоммита);
	КонецЕсли;
	
	Если Поле = Элементы.КоммитыКоммит Тогда
		SslCI_РаботаСГитом.ОткрытьКоммит(ТекДанные.ИдКоммита);	
	КонецЕсли;
	
	Если Поле = Элементы.КоммитыПерейтиНаКоммит Тогда
		ПерейтиНаКоммит(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиНаКоммит(Команда)
	
	ТекДанные = Элементы.Коммиты.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		SslCI_РаботаСГитом.ПерейтиНаКоммит(Файл, ИмяФайла, ТекДанные.ИдКоммита);
		ТекКоммит = ТекДанные.ИдКоммита;
		
		Если ВладелецФормы.ИмяФормы = "Справочник.ДополнительныеОтчетыИОбработки.Форма.ФормаЭлемента" Тогда
			ВладелецФормы.ПеречитатьДанныеФормы();	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти