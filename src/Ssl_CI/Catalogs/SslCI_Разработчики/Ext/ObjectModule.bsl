#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПометкаУдаления Тогда
		
		ИспользуемыеПочтовыеАдреса = АдресаПочты.ВыгрузитьКолонку("Мыло");
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Почта", ИспользуемыеПочтовыеАдреса);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	SslCI_РазработчикиАдресаПочты.Мыло КАК Мыло,
		|	SslCI_РазработчикиАдресаПочты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.SslCI_Разработчики.АдресаПочты КАК SslCI_РазработчикиАдресаПочты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.SslCI_Разработчики КАК SslCI_Разработчики
		|		ПО SslCI_РазработчикиАдресаПочты.Ссылка = SslCI_Разработчики.Ссылка
		|ГДЕ
		|	SslCI_РазработчикиАдресаПочты.Ссылка <> &Ссылка
		|	И SslCI_РазработчикиАдресаПочты.Мыло В(&Почта)
		|	И НЕ SslCI_Разработчики.ПометкаУдаления";
		
		Выборка = Запрос.Выполнить().Выбрать(); 
		
		ШаблонСообщения = НСтр("ru = 'Почтовый адрес ""%1"" уже используется для разработчика ""%2""'");
		
		Пока Выборка.Следующий() Цикл
			ТекстСообщения 	= СтрШаблон(ШаблонСообщения, Выборка.Мыло, Выборка.Ссылка);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Выборка.Ссылка, , , Отказ);			
		КонецЦикла;		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли